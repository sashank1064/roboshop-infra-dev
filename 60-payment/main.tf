resource "aws_lb_target_group" "payment" {
  name     = "${var.project}-${var.environment}-payment" #roboshop-dev-payment
  port     = 8080
  protocol = "HTTP"
  vpc_id   = local.vpc_id
  deregistration_delay = 120

  health_check {
    healthy_threshold = 2
    interval = 5
    matcher = "200-299"
    path = "/health"
    port = 8080
    protocol = "HTTP"
    timeout = 2
    unhealthy_threshold = 3
  }
}

resource "aws_instance" "payment" {
    ami                    = local.ami_id
    instance_type          = "t3.micro"
    vpc_security_group_ids = [local.payment_sg_id]
    subnet_id = local.private_subnet_ids
         
    tags = merge(
        local.common_tags, 
        {
            Name = "${var.project}-${var.environment}-payment"
        }
    )
}

resource "terraform_data" "payment" {
  triggers_replace = [
    aws_instance.payment.id
  ]

  
  provisioner "file" {
    source      = "payment.sh"
    destination = "/tmp/payment.sh"
  }

  connection {
    type     = "ssh"
    user     = "ec2-user"
    password = "DevOps321"
    host     = aws_instance.payment.private_ip
  }
  

  provisioner "remote-exec" {
    inline = [
      "chmod +x /tmp/payment.sh",
      "sudo /tmp/payment.sh payment ${var.environment}"
    ]
  }
}

resource "aws_ec2_instance_state" "payment" {
  instance_id = aws_instance.payment.id
  state       = "stopped"
  depends_on = [ terraform_data.payment ]
  
}

resource "aws_ami_from_instance" "payment" {
  name               = "${var.project}-${var.environment}-payment"
  source_instance_id = aws_instance.payment.id
  depends_on = [ aws_ec2_instance_state.payment ]
  tags = merge(
    local.common_tags, 
    {
        Name = "${var.project}-${var.environment}-payment"
    }
    )
}

resource "terraform_data" "payment_delete" {
  triggers_replace = [
    aws_instance.payment.id
  ]
  # make sure you have aws configure in laptop
  provisioner "local-exec" {
    command = "aws ec2 terminate-instances --instance-ids ${aws_instance.payment.id}"
  }
  
  depends_on = [aws_ami_from_instance.payment]
  
}

resource "aws_launch_template" "payment" {
  name = "${var.project}-${var.environment}-payment"
  image_id = aws_ami_from_instance.payment.id
  instance_initiated_shutdown_behavior = "terminate"
  instance_type = "t3.micro"
  vpc_security_group_ids = [local.payment_sg_id]
  update_default_version = true # each time we update the launch template, it will create new version and make it default
  tag_specifications {
    resource_type = "instance"
# EC2 tags created by ASG
    tags = merge(
      local.common_tags, 
      {
          Name = "${var.project}-${var.environment}-payment"
      }
    )
  }
# Volume tags created by ASG
  tag_specifications {
    resource_type = "volume"

    tags = merge(
      local.common_tags, 
      {
          Name = "${var.project}-${var.environment}-payment"
      }
    )
  }
# launch specification tags
  tags = merge(
    local.common_tags, 
    {
        Name = "${var.project}-${var.environment}-payment"
    }
  )
}


resource "aws_autoscaling_group" "payment" {
  name                      = "${var.project}-${var.environment}-payment"
  max_size                  = 10
  min_size                  = 1
  desired_capacity          = 1
  vpc_zone_identifier       = local.private_subnet_id
  target_group_arns         = [aws_lb_target_group.payment.arn]
  health_check_type         = "ELB"
  health_check_grace_period = 90
  
  launch_template {
    id      = aws_launch_template.payment.id
    version = aws_launch_template.payment.latest_version
  }

  dynamic "tag" {
    for_each = merge(
      local.common_tags, 
      {
      Name = "${var.project}-${var.environment}-payment"
    }
    )
    content {
      key                 = tag.key
      value               = tag.value
      propagate_at_launch = true
    }
  }

  instance_refresh {
    strategy = "Rolling"
    preferences {
      min_healthy_percentage = 50
    }
    triggers = [ "launch_template" ]
  }

  timeouts {
    delete = "15m"
  }
}


resource "aws_autoscaling_policy" "payment" {
  name                   = "${var.project}-${var.environment}-payment"
  autoscaling_group_name = aws_autoscaling_group.payment.name
  policy_type            = "TargetTrackingScaling"
  target_tracking_configuration {
    predefined_metric_specification {
      predefined_metric_type = "ASGAverageCPUUtilization"
    }
    target_value = 75.0
  }
  
}

resource "aws_lb_listener_rule" "payment" {
  listener_arn = local.backend_alb_listener_arn
  priority     = 10

  action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.payment.arn
  }
  condition {
    host_header {
      values = ["payment.backend-${var.environment}.${var.zone_name}"]
    }
  }
}